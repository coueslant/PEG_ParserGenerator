Program : Block "$"
Block : "{" StatementList "}"
StatementList : Statement StatementList | ""
Statement : PrintStatement | AssignmentStatement | VarDecl | WhileStatement | IfStatement | Block
PrintStatement : "print" "(" Expr ")"
AssignmentStatement : Id "=" Expr
VarDecl : type Id
WhileStatement : "while" BooleanExpr Block
IfStatement : "if" BooleanExpr Block
Expr : IntExpr | StringExpr | BooleanExpr | Id
IntExpr : digit intop Expr | digit
StringExpr : "\"" CharList "\""
BooleanExpr : "(" Expr boolop Expr ")" | boolval
Id : charLiteral
CharList : charLiteral CharList | space CharList | ""
type : "int" | "string" | "boolean" 
charLiteral : "a" | "b" | "c" 
space : " "
digit : "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
boolop : "==" | "!="
boolval : "false" | "true"
intop : "+"
